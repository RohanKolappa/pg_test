; Script generated by the HM NIS Edit Script Wizard.
;!define SCRIPT_DEBUG 1
; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Barco Collaboration Client"
!ifndef PRODUCT_VERSION
!define PRODUCT_VERSION "1.0.r0.b0"
!endif
!define PRODUCT_PUBLISHER "Barco Inc"
!define PRODUCT_WEB_SITE "http://www.ipvideosys.com"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\IPVS_Collaboration_Client"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\IPVS Collaboration Client"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

!include "MUI2.nsh"
!include "FileFunc.nsh"
!include "WordFunc.nsh"
!include "IPVSFilePaths.nsh"
!include "IPVSCommonFunc.nsh"
!include "IPVSGuiFunc.nsh"
!include "IPVSCommonSections.nsh"
!include "IPVSMajorUpgrade.nsh"

RequestExecutionLevel highest

!insertmacro SETOUTFILE "BarcoCollabClientSetup"
Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
InstallDir "$PROGRAMFILES\${PRODUCT_PUBLISHER}\${PRODUCT_NAME}"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
!ifdef SCRIPT_DEBUG
ShowInstDetails show
ShowUnInstDetails show
!else 
ShowInstDetails hide
ShowUnInstDetails hide
!endif
CRCCheck force
BrandingText " "

Var CollabClientInstalled

!macro IF_IPVSINSTALLED_SET_UPDATEFLAG
  Push $0

  !insertmacro RESETUPDATEFLAG "barco"
  !insertmacro IFDIREXISTS ${CLIENT_DSTDIR} $0
  ${If} $0 == "1"
    !insertmacro SETUPDATEFLAG "barco"
  ${EndIf}
  !insertmacro IFDIREXISTS ${ADMIN_DSTDIR} $0
  ${If} $0 == "1"
    !insertmacro SETUPDATEFLAG "barco"
  ${EndIf}
  !insertmacro IFDIREXISTS ${CLIENT_VIEWER_DSTDIR} $0
  ${If} $0 == "1"
    !insertmacro SETUPDATEFLAG "barco"
  ${EndIf}

  Pop $0
!macroend

!define MUI_CUSTOMFUNCTION_GUIINIT myGUIInit

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON ${IPVSICON}
!define MUI_UNICON ${IPVSICON}

; Welcome page
!define MUI_PAGE_CUSTOMFUNCTION_PRE IPVSPagePreFunc
!insertmacro MUI_PAGE_WELCOME

; License page
!define MUI_PAGE_CUSTOMFUNCTION_PRE IPVSPagePreFunc
!insertmacro MUI_PAGE_LICENSE "EULA.rtf"

; Directory page
!define MUI_PAGE_CUSTOMFUNCTION_PRE IPVSPagePreFunc
!define MUI_PAGE_CUSTOMFUNCTION_SHOW IPVSDirPageShowFunc
!insertmacro MUI_PAGE_DIRECTORY

; Component selection page
!define MUI_PAGE_CUSTOMFUNCTION_PRE IPVSPagePreFunc
!insertmacro MUI_PAGE_COMPONENTS

; Install Options page (e.g. shortcuts etc.)
Page custom IPVSOptionsDialogsPage OptionsDialogsPageLeave

; Update Page (whether this is a update)
Page custom IPVSUpdateDialogPage IPVSUpdateDialogPageLeave 

; Instfiles page
!insertmacro MUI_PAGE_INSTFILES

; Finish page
;!define MUI_FINISHPAGE_RUN "mshta.exe"
;!define MUI_FINISHPAGE_RUN_PARAMETERS "$\"${SETTINGS_SHORTCUT_TARGET}$\""
;!define MUI_FINISHPAGE_RUN_TEXT "Launch the Settings page"
!define MUI_PAGE_CUSTOMFUNCTION_SHOW IPVSFinishPageShowFunc
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

InstType /COMPONENTSONLYONCUSTOM 
InstType "Complete"

Section "-JRE" SEC_JRE
  SetDetailsPrint listonly
  !insertmacro JRESECTION
SectionEnd

Section "-Common" SEC_COMMON
  SetDetailsPrint listonly

  Push $1

  !insertmacro CHECKUPDATEFLAG "barco" $1
  !insertmacro COMMONSECTION $1 $CBDesktop_State $CBStartMenu_State

  Pop $1
SectionEnd

Section "Sync Media Player" SEC_VIEWERCLIENT
  SetDetailsPrint listonly
  SectionIn 1

  Push $1

  SetOutPath "$INSTDIR"
  SetOverwrite on

  Delete "$INSTDIR\${CLIENTVIEWERICON}"
  File ${CLIENTVIEWERICON}
  
  ;add .htm file to root
  SetOutPath "${GI_DSTDIR}"
  Delete "${GI_DSTDIR}\${CLIENT_VIEWER_INDEX}"
  File "${GI_SRCDIR}\${CLIENT_VIEWER_INDEX}"
  
  ;add all jar files to root for applet loading from .htm
  SetOutPath "${GI_DSTDIR}"
  Delete "${GI_DSTDIR}\${CLIENT_VIEWER_JARS}"
  File "${CLIENT_VIEWER_JARS}"
  
  SetOutPath "${JSXAPPS_DSTDIR}"
  RMDir /r "${CLIENT_VIEWER_DSTDIR}"
  File /r /x .svn "${CLIENT_VIEWER_SRCDIR}"

  !insertmacro CHECKUPDATEFLAG "barco" $1

  !ifdef SCRIPT_DEBUG
    MessageBox MB_OK "CHECKUPDATEFLAG barco: $1 ,CBStartMenu_State :$CBStartMenu_State, CBDesktop_State:$CBDesktop_State , BST_CHECKED :${BST_CHECKED}"
  !endif
  
  ;new install Viewer client 
  ${If} $1 == "0"
      ${If} $CBStartMenu_State == ${BST_CHECKED}
        !insertmacro CREATESTARTMENUSHORTCUT "${PRODUCT_NAME}" \
          "${CLIENT_VIEWER_LNK}" \
          "'$PROGRAMFILES\Internet Explorer\iexplore.exe'"\
          "${CLIENT_VIEWER_SHORTCUT_TARGET}" \
          "$INSTDIR\${CLIENTVIEWERICON}"
      ${EndIf}

      ${If} $CBDesktop_State == ${BST_CHECKED}
        !insertmacro CREATEDESKTOPSHORTCUT "${CLIENT_VIEWER_LNK}" \
          "'$PROGRAMFILES\Internet Explorer\iexplore.exe'" \
          "${CLIENT_VIEWER_SHORTCUT_TARGET}" "$INSTDIR\${CLIENTVIEWERICON}"
      ${EndIf}
  ${EndIf}
  

  Pop $1
SectionEnd

Section "Client" SEC_IPVSCLIENT
  SetDetailsPrint listonly
  SectionIn 1

  Push $1

  SetOutPath "$INSTDIR"
  SetOverwrite on

  Delete "$INSTDIR\${IPVSICON}"
  File ${IPVSICON}

  SetOutPath "${GI_DSTDIR}"
  Delete "${GI_DSTDIR}\${CLIENT_INDEX}"
  File "${GI_SRCDIR}\${CLIENT_INDEX}"

;  Delete "${GI_DSTDIR}\${CLIENT_HTML_INDEX}"
;  File "${GI_SRCDIR}\${CLIENT_HTML_INDEX}"
  
  SetOutPath "${JSXAPPS_DSTDIR}"
  RMDir /r "${CLIENT_DSTDIR}"
  File /r /x .svn "${CLIENT_SRCDIR}"
  
  !insertmacro CHECKUPDATEFLAG "barco" $1
  ${If} $1 == "0"
  
    SetShellVarContext all

    ${If} $CBStartMenu_State == ${BST_CHECKED}
      !insertmacro CREATESTARTMENUSHORTCUT "${PRODUCT_NAME}" \
          "${CLIENT_LNK}" "${CLIENT_SHORTCUT_TARGET}" "" \
          "$INSTDIR\${IPVSICON}"
    ${EndIf}

    ${If} $CBDesktop_State == ${BST_CHECKED}
      !insertmacro CREATEDESKTOPSHORTCUT "${CLIENT_LNK}" \
          "${CLIENT_SHORTCUT_TARGET}" "" "$INSTDIR\${IPVSICON}"
    ${EndIf}
    
  ${EndIf}

  Pop $1
SectionEnd

Section "Admin" SEC_ADMIN
  SetDetailsPrint listonly
  SectionIn 1

  Push $1

  SetOutPath "$INSTDIR"
  SetOverwrite on

  Delete "$INSTDIR\${IPVSADMINICON}"
  File ${IPVSADMINICON}

  ;put IPVSAdmin-Index.hta under GI
  SetOutPath "${GI_DSTDIR}"
  Delete "${GI_DSTDIR}\${ADMIN_INDEX}"
  File "${GI_SRCDIR}\${ADMIN_INDEX}"
  
  ;  Delete "${GI_DSTDIR}\${ADMIN_HTML_INDEX}"
  ;  File "${GI_SRCDIR}\${ADMIN_HTML_INDEX}"
  
  SetOutPath "${JSXAPPS_DSTDIR}"
  RMDir /r "${ADMIN_DSTDIR}"
  File /r /x .svn "${ADMIN_SRCDIR}"

  !insertmacro CHECKUPDATEFLAG "barco" $1
  ${If} $1 == "0"

    SetShellVarContext all

    ${If} $CBStartMenu_State == ${BST_CHECKED}
      !insertmacro CREATESTARTMENUSHORTCUT "${PRODUCT_NAME}" "${ADMIN_LNK}" \
          "${ADMIN_SHORTCUT_TARGET}" "" "$INSTDIR\${IPVSADMINICON}"
    ${EndIf}

    ${If} $CBDesktop_State == ${BST_CHECKED}
      !insertmacro CREATEDESKTOPSHORTCUT "${ADMIN_LNK}" \
          "${ADMIN_SHORTCUT_TARGET}" "" "$INSTDIR\${IPVSADMINICON}"
    ${EndIf}

  ${EndIf}

  Pop $1
SectionEnd


Section "-Player Section" SEC_PLAYER

  SetDetailsPrint listonly

  !insertmacro INSTALLPLAYER ${PLAYER_SRCDIR} ${PLAYER_DSTDIR} ${PLAYER_FILE} \
    ${MULTIMONS_FILE} $CollabClientInstalled

SectionEnd

Section -Post

  SetDetailsPrint listonly

  WriteUninstaller "$INSTDIR\uninst.exe"
  
  ${If} $CollabClientInstalled == "0"
    WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR"
    WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" \
        "DisplayName" "${PRODUCT_NAME}"
    WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" \
        "UninstallString" "$INSTDIR\uninst.exe"
    WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" \
        "DisplayIcon" "$INSTDIR\${IPVSICON}"
    ; Not writing product version as workaround for bug 14567
    ;WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" \
    ;    "DisplayVersion" "${PRODUCT_VERSION}"
    WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" \
        "URLInfoAbout" "${PRODUCT_WEB_SITE}"
    WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" \
        "Publisher" "${PRODUCT_PUBLISHER}"
    WriteRegDWORD ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" \
        "NoModify" "1"
    WriteRegDWORD ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" \
        "NoRepair" "1"
  ${EndIf}
  
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "${PRODUCT_NAME} was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove ${PRODUCT_NAME} and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall

  SetDetailsPrint listonly

  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\*.ico"
  
  ; GI section
  RMDir /r /REBOOTOK "${GI_DSTDIR}"
  
  ; Player section
  
  !insertmacro UNINSTALLPLAYER ${PLAYER_DSTDIR} ${PLAYER_FILE} ${MULTIMONS_FILE}

  SetShellVarContext all
  ; Shortcuts, etc.
  !insertmacro DELETESTARTMENUSHORTCUT "${PRODUCT_NAME}" "${CLIENT_LNK}"
  !insertmacro DELETESTARTMENUSHORTCUT "${PRODUCT_NAME}" "${CLIENT_VIEWER_LNK}"
  !insertmacro DELETESTARTMENUSHORTCUT "${PRODUCT_NAME}" "${ADMIN_LNK}"

  RMDir /r /REBOOTOK "$SMPROGRAMS\${PRODUCT_NAME}"
  
  !insertmacro DELETEDESKTOPSHORTCUT "${CLIENT_LNK}"
  !insertmacro DELETEDESKTOPSHORTCUT "${CLIENT_VIEWER_LNK}"
  !insertmacro DELETEDESKTOPSHORTCUT "${ADMIN_LNK}"

  !insertmacro DELETEINSTDIR "${PRODUCT_PUBLISHER}"
  !insertmacro DELETECLIENT_REGKEYS
  Call un.RefreshDesktopWindow

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd

Function myGUIInit
  Push $R0
  Push $R3
  Push $R4

  Call IsCollabClientInstalled
  Pop $R0
  StrCpy $CollabClientInstalled $R0

  Call IsUserAdmin
  Pop $R0
  
  StrCpy $R3 "You must have administrator privileges to install ${PRODUCT_NAME}, Aborting installation"
  ${If} $CollabClientInstalled == "0"
    ${If} $R0 != "true"
      MessageBox MB_OK "$R3"
      Abort $R3
    ${EndIf}
  ${EndIf}

  ${If} $CollabClientInstalled == "0"
    ; Change default installation dir to root drive
    ${GetRoot} $PROGRAMFILES $R0
    StrCpy $INSTDIR "$R0\${PRODUCT_PUBLISHER}\${PRODUCT_NAME}"
  ${Else}
    !insertmacro BACKUPFILE "${PREFS_DSTFILE}" "${IPVSDATADIR}"
    ;BACKUP Sync Media Player preference.xml as well
    !ifdef SCRIPT_DEBUG
           MessageBox MB_OK "Viewer back up dir: ${VIEWERDATADIR}"
    !endif
    !insertmacro BACKUPFILE "${VPREFS_DSTFILE}" "${VIEWERDATADIR}"
    
    !insertmacro GETPRODUCTVERSION ${VERSION_DSTFILE} $R3
    !insertmacro COMPAREVERSION ${IPVSUPGRADE_VERSION} $R3 $R4    
    ${If} $R4 == 1
        StrCpy $R4 "This update of ${PRODUCT_NAME} requires that you first uninstall the previous version."
        MessageBox MB_OK "$R4"
        Abort $R4
    ${EndIf}
  ${EndIf}

  !insertmacro IF_IPVSINSTALLED_SET_UPDATEFLAG

  ; Make the STARTMENU shortcut option on by default in the Options dialog
  ; page
  StrCpy $CBStartMenu_State ${BST_CHECKED}
  
  Pop $R4
  Pop $R3
  Pop $R0
FunctionEnd

Function IPVSUpdateDialogPage
  Push $0
  Push $1
  
  !insertmacro CHECKUPDATEFLAG "barco" $0
  ${If} $0 == "0"
      Pop $0
      Abort 
  ${EndIf}

  !insertmacro HIDEBACKBUTTON

  !insertmacro MUI_HEADER_TEXT "Update Wizard" \
  "Setup has detected that ${PRODUCT_NAME} is already installed. Proceed with update?"

  ;always 1018 simply required by MUI2 -- nsDialogs::Create
  nsDialogs::Create 1018
  Pop $0

  ${If} $0 == error
    Abort
  ${EndIf}

  nsDialogs::Show

  ; Update only the components that are already installed
  !insertmacro SELECTCOMPONENTIFEXISTS ${SEC_IPVSCLIENT} ${CLIENT_DSTDIR}
  !insertmacro SELECTCOMPONENTIFEXISTS ${SEC_ADMIN} ${ADMIN_DSTDIR}

  ; Check whether Viewer client installed or not
  !insertmacro IFDIREXISTS ${CLIENT_VIEWER_DSTDIR} $1
  ${If} $1 == "1"
        !ifdef SCRIPT_DEBUG
           MessageBox MB_OK "${CLIENT_VIEWER_DSTDIR} exist"
        !endif
        !insertmacro SELECTCOMPONENTIFEXISTS ${SEC_VIEWERCLIENT} ${CLIENT_VIEWER_DSTDIR}
  ${Else}
        !insertmacro SELECTCOMPONENT ${SEC_VIEWERCLIENT}
        !ifdef SCRIPT_DEBUG
           MessageBox MB_OK "${CLIENT_VIEWER_DSTDIR} not exist"
        !endif
  ${EndIf}

  Pop $1
  Pop $0
FunctionEnd

Function IPVSUpdateDialogPageLeave

FunctionEnd

Function IPVSDirPageShowFunc

  Var /GLOBAL hwnd
  Var /GLOBAL item

  ${If} $CollabClientInstalled == "1"
    FindWindow $hwnd "#32770" "" $HWNDPARENT
    GetDlgItem $item $hwnd 1019
    EnableWindow $item 0

    GetDlgItem $item $hwnd 1020
    EnableWindow $item 0

    GetDlgItem $item $hwnd 1001
    EnableWindow $item 0
  
    GetDlgItem $item $hwnd 1006
    SendMessage $item ${WM_SETTEXT} 0 "STR:Files will be installed in the below folder because another collaboration client is already installed there."
  ${EndIf}
FunctionEnd

Function IPVSPagePreFunc
    Push $0
    
    !insertmacro CHECKUPDATEFLAG "barco" $0
    ${If} $0 == "1"
        Pop $0
        Abort 
    ${EndIf}
    
    Pop $0
FunctionEnd

Function IPVSOptionsDialogsPage

  Push $0
  !insertmacro CHECKUPDATEFLAG "barco" $0
  ${If} $0 == "1"
      Pop $0
      Abort 
  ${EndIf}

  Pop $0

  Call OptionsDialogsPage

FunctionEnd

Function .onMouseOverSection 
    
    Push $R0
    Push $R1

    FindWindow $R0 "#32770" "" $HWNDPARENT 
    GetDlgItem $R0 $R0 1043 ; description item 
  
    CreateFont $R1 "Arial" 9 400 ; font for mouse over descriptions
    SetCtlColors $R0 0x000000 ; set colors
    SendMessage $R0 ${WM_SETFONT} $R1 0

    ${If} $0 == ${SEC_IPVSCLIENT}
        SendMessage $R0 ${WM_SETTEXT} 0 "STR: This will install the Client"
    ${ElseIf} $0 == ${SEC_ADMIN}
        SendMessage $R0 ${WM_SETTEXT} 0 "STR: Ths will install the Admin UI"
    ${ElseIf} $0 == ${SEC_VIEWERCLIENT}
        SendMessage $R0 ${WM_SETTEXT} 0 "STR: This will install the Sync Media Player"
    ${ElseIf} $0 == -1
        SendMessage $R0 ${WM_SETTEXT} 0 "STR: Position your mouse over a component to see its description."
    ${EndIf}

    Pop $R1
    Pop $R0
FunctionEnd

Function IPVSFinishPageShowFunc
    Push $0
    
    !insertmacro CHECKUPDATEFLAG "barco" $0
    ${If} $0 == "1"
        !insertmacro RESTOREFILE "${VPREFS_DSTFILE}" "${VIEWERDATADIR}"
        SendMessage $mui.Finishpage.Text ${WM_SETTEXT} 0 \
            "STR:Updates have been successfully applied."
        EnableWindow $mui.Finishpage.Run 0
    ${EndIf}
    
    Pop $0
FunctionEnd

